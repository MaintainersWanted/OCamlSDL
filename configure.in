dnl ******
dnl OCamlSdl autoconf input
dnl ******
dnl
dnl $Id: configure.in,v 1.15 2002/05/27 22:37:39 xtrm Exp $

dnl  begin configure.in 
AC_INIT(src/sdl.ml)
AC_PREFIX_DEFAULT(/usr)

AC_CONFIG_AUX_DIR(./support)
# need support/install-sh
# need support/missing
# need support/mkinstalldirs

dnl -*- Check for programs -*-
AC_PROG_CC
AC_PROG_INSTALL
AC_PATH_PROG(CP, cp, 
	      AC_MSG_ERROR(Cannot find cp.))

#  Check for OCaml programs 
AC_PATH_PROG(OCAMLC,ocamlc,no)
if test "$OCAMLC" = no ; then
	AC_MSG_ERROR(Cannot find ocamlc.)
fi
AC_PATH_PROG(OCAMLOPT,ocamlopt,no)
if test "$OCAMLOPT" = no ; then
	AC_MSG_ERROR(Cannot find ocamlopt.)
fi
AC_PATH_PROG(OCAMLMKTOP,ocamlmktop,no)
if test "$OCAMLMKTOP" = no ; then
	AC_MSG_ERROR(Cannot find ocamlmktop.)
fi
AC_PATH_PROG(OCAMLDEP,ocamldep,no)
if test "$OCAMLDEP" = no ; then
	AC_MSG_ERROR(Cannot find ocamldep.)
fi

AC_SUBST(OCAMLC)
AC_SUBST(OCAMLOPT)
AC_SUBST(OCAMLMKTOP)
AC_SUBST(OCAMLDEP)

dnl -*- Check for libraries -*-

dnl  Figure out which math library to use 
case "$target" in
    *mingw32*)
        MATHLIB=""
        ;;
    *beos*)
        MATHLIB=""
        ;;
    *)
        MATHLIB="-lm"
        ;;
esac
AC_SUBST(MATHLIB)


dnl  Check for SDL libraries 
AC_PATH_PROG(SDL_CONFIG,sdl-config,no)
if test "$SDL_CONFIG" = no ; then
        AC_MSG_ERROR(Cannot find sdl-config.)
else
        SDL_CFLAGS=`$SDL_CONFIG --cflags`
        SDL_LIBS=`$SDL_CONFIG --libs`
fi

LIBS="$LIBS $SDL_LIBS"
CPPFLAGS="$CPPFLAGS $SDL_CFLAGS"

dnl Checking SDL headers
AC_CHECK_HEADER(SDL/SDL.h,, AC_MSG_ERROR(Cannot find headers of SDL.h.))

dnl  cmdline options 
AC_ARG_ENABLE(sdl_ttf,
 	      [  --disable-sdl-ttf       without freetype support  [default is enabled]], sdl_ttf=$enableval, sdl_ttf=yes)
AC_ARG_ENABLE(sdl_image,
 	      [  --disable-sdl-image     without sdl image support [default is enabled]], sdl_image=$enableval, sdl_image=yes)
AC_ARG_ENABLE(sdl_mixer,
 	      [  --disable-sdl-mixer     without sdl mixer support [default is enabled]], sdl_mixer=$enableval, sdl_mixer=yes)
AC_SUBST(PARTS)

dnl -*- Checking for lib SDL_ttf -*-
if test x"$sdl_ttf" = x"yes"
then
  AC_CHECK_HEADERS(SDL/SDL_ttf.h, have_sdl_ttf="true", have_sdl_ttf="false")
  if test "$have_sdl_ttf" = true;
  then
    AC_CHECK_LIB(SDL_ttf, TTF_Init, ttf_ok=yes, ttf_ok=no)
    if test "$ttf_ok" = no;
    then
      AC_MSG_ERROR(Cannot find SDL_tff library.)
    else
      LIBS="$LIBS -lSDL_ttf"
      PARTS="ttf"
    fi
  else
    sdl_ttf=no
  fi
fi

dnl -*- Checking for lib SDL_image -*-
if test x"$sdl_image" = x"yes"
then
  AC_CHECK_HEADERS(SDL/SDL_image.h, 
                   have_sdl_image="true",
		   have_sdl_image="false")
  if test "$have_sdl_image" = true;
  then
    AC_CHECK_LIB(SDL_image, IMG_Load_RW, image_ok=yes, image_ok=no)
    if test "$image_ok" = no; 
    then 
      AC_MSG_ERROR(Cannot find SDL_image library.)
    else
      LIBS="$LIBS -lSDL_image"
      PARTS="$PARTS loader"
    fi
  else
    sdl_image=no
  fi
fi

dnl -*- Checking for lib SDL_mixer -*-
if test x"$sdl_mixer" = x"yes"
then 
  AC_CHECK_HEADERS(SDL/SDL_mixer.h, 
	           have_sdl_mixer="true", 
		   have_sdl_mixer="false")
  if test "$have_sdl_mixer" = true;
  then
    AC_CHECK_LIB(SDL_mixer, Mix_OpenAudio, mixer_ok=yes, mixer_ok=no)
    if test "$mixer_ok" = no; 
    then 
      AC_MSG_ERROR(Cannot find SDL_mixer library.)
    else
      LIBS="$LIBS -lSDL_mixer"
      PARTS="$PARTS mixer"
    fi
  fi
fi

dnl -*- Checking for libpng -*-
if test x"$sdl_image" = x"yes"
then 
  AC_CHECK_HEADER(png.h,,AC_MSG_ERROR(Cannot find png.h.))
  AC_CHECK_LIB(png, png_read_image,
	       png_ok=yes,
               AC_MSG_ERROR(Cannot find libpng.),
	       -lz -lm)
  LIBS="$LIBS -lpng -lz"
fi

dnl  Check for typedefs 
dnl  Check for structures 
dnl  Check for compiler characteristics 
dnl  Check for library functions 

dnl  Check for system services 

#  Finally create all the generated files 
AC_OUTPUT(Makefile makefile.toplevel samples/Makefile src/Makefile lib/Makefile doc/Makefile bin/Makefile bin/ocamlsdl )
